plugins {
	id 'java'
	id 'org.springframework.boot' version '2.7.9'
	id 'io.spring.dependency-management' version '1.0.15.RELEASE'
	// restdocs
	id "org.asciidoctor.jvm.convert" version "3.3.2"
	id "com.epages.restdocs-api-spec" version "0.16.2"
}

group = 'sku'
version = '0.0.1-SNAPSHOT'
sourceCompatibility = '11'

configurations {
	asciidoctorExt
	compileOnly {
		extendsFrom annotationProcessor
	}
}

repositories {
	mavenCentral()
}

ext {
	set("snippetsDir", file("build/generated-snippets"))
}

dependencies {
	implementation 'org.springframework.boot:spring-boot-starter-data-jpa'
	implementation 'org.springframework.boot:spring-boot-starter-web'
	implementation 'org.springframework.boot:spring-boot-starter-webflux'
	implementation 'io.netty:netty-resolver-dns-native-macos:4.1.79.Final:osx-aarch_64'
	// swagger(openapi)
	implementation("org.springdoc:springdoc-openapi-ui:1.6.11")
	implementation("org.springdoc:springdoc-openapi-webmvc-core:1.6.11")

	// spring security
	implementation 'org.springframework.boot:spring-boot-starter-security'
	implementation 'io.jsonwebtoken:jjwt:0.9.1'
	implementation "org.springframework.boot:spring-boot-configuration-processor"
	// oauth2
	implementation 'org.springframework.boot:spring-boot-starter-oauth2-client'
	// redis
	implementation 'org.springframework.boot:spring-boot-starter-data-redis'
	// QueryDSL JPA
	implementation 'com.querydsl:querydsl-jpa'
	implementation 'com.querydsl:querydsl-apt'
	// QFile 생성 및 가져오기
	annotationProcessor "com.querydsl:querydsl-apt:${dependencyManagement.importedProperties['querydsl.version']}:jpa"
	annotationProcessor "jakarta.persistence:jakarta.persistence-api"
	annotationProcessor "jakarta.annotation:jakarta.annotation-api"

	compileOnly 'org.projectlombok:lombok'
	runtimeOnly 'com.mysql:mysql-connector-j'
	annotationProcessor 'org.projectlombok:lombok'
	testImplementation 'org.springframework.boot:spring-boot-starter-test'
	// restdocs
	testImplementation("org.springframework.restdocs:spring-restdocs-mockmvc")
	testImplementation("com.epages:restdocs-api-spec:0.16.2")
	testImplementation("com.epages:restdocs-api-spec-mockmvc:0.16.2")

	asciidoctorExt("org.springframework.restdocs:spring-restdocs-asciidoctor")
}

tasks.named('test') {
	outputs.dir snippetsDir
	useJUnitPlatform()
}

tasks.register("restDocsTest", Test) { // ./gradlew clean restDocsTest 시 restDocs 파일 생성
	outputs.dir snippetsDir
	useJUnitPlatform {
		includeTags("restDocs")
	}
	finalizedBy "asciidoctor"
	finalizedBy "openapi3"
}

tasks.named("asciidoctor") {
	configurations 'asciidoctorExt'
	baseDirFollowsSourceFile()
	inputs.dir snippetsDir
	dependsOn restDocsTest
}

openapi3 {
	servers = [
			{ url = 'http://localhost:8080' },
	]
	title = 'spring-rest-docs-guide'
	description = 'Spring REST Docs 테스트 생성물 생성시 추가생성되는 OpenAPI 문서이용'
	version = "${project.version}"
	format = 'yaml'
}

tasks.register("apiBuild", GradleBuild) { // ./gradlew apiBuild 시 restdocs 생성 후 build
	tasks = ["clean", "restDocsTest", "build"]
}

tasks.named("bootBuildImage") { // ./gradlew bootBuildImage 시 docker hub로 배포
	imageName = project.DOCKER_ID + "/" + project.DOCKER_IMAGE_NAME
	publish = true
	docker {
		publishRegistry {
			username = project.DOCKER_ID
			password = project.DOCKER_PW
		}
	}
}

bootJar {
	from("swagger-ui") {
		into "BOOT-INF/classes/static/swagger"
	}
	from("${asciidoctor.outputDir}") {
		into "BOOT-INF/classes/static/docs"
	}
	from("build/api-spec") {
		into "BOOT-INF/classes/static/swagger"
	}

	archiveFileName.set "moamoa.jar"
}

springBoot {
	buildInfo()
}
